initialize() {
    initializeSLiMModelType("nonWF");
    initializeSLiMOptions(keepPedigrees=T, dimensionality="xy");
    initializeTreeSeq();
    if(exists("slimgui")) {
    	defineConstant("PWD", "/home/christ/PycharmProjects/gaia_temporal_testing/simulations_bash/");
    	defineConstant("REP", 1);
    }
    defineConstant("TPREFIX", "tree-");
    defineConstant("LPREFIX", "landscape-");
    defineConstant("TREEFILE", paste(PWD) + "/trees/" + TPREFIX + paste(REP) + ".trees");
    defineConstant("LANDSCAPEFILE", paste(PWD) + "/landscapes/" + LPREFIX + paste(REP) + ".csv");
    defineConstant("LOG_FILE", paste(PWD) + "/logs/SLiM_logs/" + TPREFIX + paste(REP) + ".txt");
    defineConstant("SAMPLES_PER_INTERVAL", 250);
    defineConstant("NUM_INTERVALS", 4);
    defineConstant("GENERATIONS", 10000);
    defineConstant("NUM_INDS", 9000);
    defineConstant("NUM_PER_GEN", asInteger((SAMPLES_PER_INTERVAL*NUM_INTERVALS)/(NUM_INDS*GENERATIONS))+1);
    defineConstant("PERCENT_PER_GEN", 0.01);
    // width of the simulated area 
    defineConstant("W", 25.0);
    // interaction width
    defineConstant("S", 0.025);
    // genome length
    defineConstant("G", 1e8);
    // mean fecundity
    defineConstant("R", 2);
    initializeMutationType("m1", 0.5, "g", 0.0, 2);
    initializeGenomicElementType("g1", m1, 1.0);
    initializeGenomicElement(g1, 0, G-1);
    initializeMutationRate(0.0);
    initializeRecombinationRate(1e-8);
    initializeInteractionType(1, "xy", reciprocal=T, maxDistance=S * 3);
}
1 early() {
    sim.addSubpop("p1", NUM_INDS);
    p1.setSpatialBounds(c(0, 0, W, W));
    i = 0;
    for (ind in p1.individuals) {
        if (asInteger(i % 30) == 0) {
            pos = p1.pointUniform();
        }
        s = 1.1;
        while (s >= 1) {
            x = 2*runif(1)-1;
            y = 2*runif(1)-1;
            s = x*x + y*y;
        }
        ind.setSpatialPosition(
            p1.pointReflected(c(pos[0] + x*3*S, pos[1] + y*3*S)));
        i = i + 1;
    }
    K_local = readCSV(LANDSCAPEFILE, colNames=F).asMatrix();
    p1.defineSpatialMap(
        "density"
        , spatiality="xy"
        , values=K_local
        , interpolate=F
        , valueRange=c(0,max(K_local))
        , colors=c(
              "#5E4FA2", "#3288BD", "#66C2A5", "#ABDDA4"
            , "#E6F598", "#FFFFBF", "#FEE08B", "#9E0142"
            , "#FDAE61", "#F46D43", "#D53E4F")
    );
    log = community.createLogFile(LOG_FILE, append=T);
    log.addCycle();
    log.addCustomColumn("pedigree_IDs", "getPedigreeIDs(NULL);");
}
2: first() {
    i1.evaluate(p1);
}
reproduction() {
    n_t = i1.totalOfNeighborStrengths(individual);
    M = p1.spatialMapValue("density", individual.spatialPosition);
    mean_litter_size = R / (1 + n_t / M);
    mate = i1.drawByStrength(individual, 1);
    if (mate.size()) {
        litterSize = rpois(1, mean_litter_size);
        for (i in seqLen(litterSize)) {
            child = subpop.addCrossed(individual, mate);
            pos = individual.spatialPosition + rposition(rpareto(S, 2.0));
            child.setSpatialPosition(p1.pointReflected(pos));
        }
    }
}
:10000 early() {
    i1.evaluate(p1);
    inds = p1.individuals;
    competition = i1.totalOfNeighborStrengths(inds);
    K_local = p1.spatialMapValue("density", inds.spatialPosition);
    inds.fitnessScaling = min(1.0, K_local / competition);
    inds[inds.age > 0].fitnessScaling = 0.0;
}

1: late() {
	sim.treeSeqRememberIndividuals(p1.individuals, permanent=F);
	if(sim.cycle >= (GENERATIONS/NUM_INTERVALS)) {
		if(sim.cycle % 10 == 0) {
			log = community.logFiles;
			log.logRow();
            print("Logged row for generation " + paste(sim.cycle));
		}
	}
}


10000 late() {
    sim.treeSeqOutput(TREEFILE);
    sim.simulationFinished();
}


function (float) rposition(float$ radius) {
    s = 1.1;
    while (s >= 1) {
        x = 2*runif(1)-1;
        y = 2*runif(1)-1;
        s = x*x + y*y;
    }
    return c(x*radius, y*radius);
}


function (float$) rpareto(float$ xmin, float$ alpha) {
    return xmin / runif(1)^(1.0 / alpha);    
}

function (string$) getPedigreeIDs(NULL n) {
		inds = p1.individuals;
		num_to_remember = asInteger(p1.individualCount*PERCENT_PER_GEN)+1;
		inds_to_remember_perm = sample(inds, num_to_remember);	
		sim.treeSeqRememberIndividuals(inds_to_remember_perm, permanent=T);
		return paste(inds_to_remember_perm.pedigreeID, sep=",");
}
